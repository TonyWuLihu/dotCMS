configurations {
    compile
    antLibs
    ext.dotcmsFolder = "dotCMS"
    ext.libsFolder = "dotCMS/WEB-INF/lib"
    ext.felixFolder = "dotCMS/WEB-INF/felix/bundle"
}
//Import and apply the dependencies from the dependencies scripts
apply from: "$rootDir/dependencies.gradle"

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
    maven { url "http://repo.dotcms.com/artifactory/libs-snapshot-local" }
}

task copyToLib(type: Copy) {
    into "."//necessary to prevent Gradle from complaining about an unspecified destination dir
    from(configurations.compile) {

        include '**/dot.org.apache.felix.bundlerepository*.jar'
        include '**/dot.org.apache.felix.fileinstall*.jar'
        include '**/dot.org.apache.felix.gogo.*.jar'
        include '**/dot.org.apache.felix.http.bundle*.jar'

        into configurations.felixFolder
    }
    from(configurations.compile) {

        exclude '**/dot.org.apache.felix.bundlerepository*.jar'
        exclude '**/dot.org.apache.felix.fileinstall*.jar'
        exclude '**/dot.org.apache.felix.gogo.*.jar'
        exclude '**/*.zip'

        into configurations.libsFolder
    }
    from(configurations.compile) {
        include '**/starter*.zip'
        into configurations.dotcmsFolder
        rename ( /starter(.+)\.zip/, "starter.zip" )
    }
}

dependencies { 
    antLibs group: 'org.apache.ant', name: 'ant-junit', version: '1.9.3'
    antLibs group: 'ant-contrib', name: 'ant-contrib', version: '1.0b3'
}

ClassLoader antClassLoader = org.apache.tools.ant.Project.class.classLoader
configurations.antLibs.each { File f -> antClassLoader.addURL(f.toURI().toURL()) }

//Move the dependencies to the WEB-INF/lib folder
tasks.copyToLib.execute()

ant.importBuild 'build.xml'

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

defaultTasks 'help', 'tasks'
